# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -e ~/.localrc ]]
then
  source ~/.localrc
fi

# Do the same for private aliases, these are mostly ones we don't want
# committed to source control.
if [[ -e ~/.localaliases ]]
then
  source ~/.localaliases
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH

  # autoload -Uz compinit
  # compinit
fi

autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Zsh syntax highlighting
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[command]='none'
ZSH_HIGHLIGHT_STYLES[builtin]='none'
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
if [[ "$OSTYPE" == "darwin"* ]]; then
  source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
else
  source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Set the GPG tty
export GPG_TTY=$(tty)

# Source asdf on mac
if [[ "$OSTYPE" == "darwin"* ]]; then
  . /usr/local/opt/asdf/asdf.sh
fi

# Source asdf on linux
if [[ "$OSTYPE" == "linux-gnu"* ]]; then
  . $HOME/.asdf/asdf.sh
fi

# Add pip3 --user installed stuff to path
export PATH=~/.local/bin:$PATH

